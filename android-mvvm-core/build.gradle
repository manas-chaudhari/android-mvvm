apply plugin: 'com.android.library'

buildscript {
    ext.moduleArtifact = 'core'
}

static def isCi() {
    return "true".equals(System.getenv('CI'))
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        String sharedTestDir = 'src/sharedTest/java'
        test {
            java.srcDir sharedTestDir
        }
        androidTest {
            java.srcDir sharedTestDir
        }
    }

    dataBinding {
        enabled = true
    }

    dexOptions {
        preDexLibraries = !isCi()
    }

    lintOptions {
        textOutput 'stdout'
        textReport true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

if (isCi()) {
    println 'Running on CI. Adding task to start emulator.'

    task startEmulator << {
        "$rootDir/gradle/start-emulator.sh".execute().waitFor()
    }

    android.testVariants.all { variant ->
        variant.connectedInstrumentTest.dependsOn startEmulator
    }
}

dependencies {
    testCompile 'junit:junit:4.12'
    compile "io.reactivex.rxjava2:rxjava:$rx_version"

    compile "com.android.support:recyclerview-v7:$support_library_version"
    compile "com.android.support:support-v4:$support_library_version"

    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile "com.android.support:recyclerview-v7:$support_library_version"
}

apply from: rootProject.file('gradle/bintray-push.gradle')
repositories {
    mavenCentral()
    jcenter()
}
