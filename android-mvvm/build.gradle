apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

def isCi() {
    return "true".equals(System.getenv('CI'))
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        String sharedTestDir = 'src/sharedTest/java'
        test {
            java.srcDir sharedTestDir
        }
        androidTest {
            java.srcDir sharedTestDir
        }
    }

    dataBinding {
        enabled = true
    }

    dexOptions {
        preDexLibraries = !isCi()
    }

    lintOptions {
        textOutput 'stdout'
        textReport true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

if (isCi()) {
    println 'Running on CI. Adding task to start emulator.'

    task startEmulator << {
        "$rootDir/gradle/start-emulator.sh".execute().waitFor()
    }

    android.testVariants.all { variant ->
        variant.connectedInstrumentTest.dependsOn startEmulator
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    provided "io.reactivex.rxjava2:rxjava:2.0.7"
    provided group: 'javax.inject', name: 'javax.inject', version: '1'

    compile 'com.android.support:recyclerview-v7:25.3.1'
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.trello:navi:1.0'
    provided "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    provided 'com.bluelinelabs:conductor:2.1.2'
}

apply from: rootProject.file('gradle/bintray-push.gradle')
repositories {
    mavenCentral()
}
